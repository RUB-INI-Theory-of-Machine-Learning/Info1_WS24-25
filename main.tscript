include "Assets";
include "Tetromino";
include "Playfield";
include "Renderer";
include "TetrominoPool";

# Initialize the necessary classes
var playfield = Playfield(10,20);
var pool = TetrominoPool();
var tetromino = Tetromino(pool.get());
var renderer = Renderer();

var level = 9;
var lines = 0;
var score = 0;

var gravity = level_gravity[level];
var previous_time = localtime();
var just_spawned = true;
var soft_drop_locked = false;
var collision_detected = false;

setEventHandler("timer", function(event){
	if localtime() - previous_time > gravity then {
		previous_time = localtime();

		# render the state so we can see changes
		renderer.render(playfield, tetromino, pool.next(), score, level, lines);

	  	# change the y position of the block
		tetromino.drop();

		# check for collision, if true then undrop tetromino, insert and get new
		if collision_detected and playfield.check_collision(tetromino) then {
			if just_spawned then quitEventMode();
			tetromino.undo();
			playfield.insert_tetromino(tetromino);

			var cleared_rows = playfield.remove_full_rows(tetromino.row);
			if cleared_rows > 0 then {
				lines += cleared_rows;
				score += base_score_values[cleared_rows] * (level + 1);
				if level <= level_gravity.size() and level < lines // 10 then level = lines // 10;

			}

			soft_drop_locked = true;
			gravity = level_gravity[level];

			tetromino = Tetromino(pool.get());
			just_spawned = true;

			collision_detected = false;
		}
		else { # no collision just after spawning
			just_spawned = false;
		}

		if playfield.check_collision(tetromino) then {
			collision_detected = true;
			tetromino.undo();
		}
	}
});

setEventHandler("canvas.keydown", function(event){
	if event.key == "ArrowRight" then tetromino.move_right();
	if event.key == "ArrowLeft" then tetromino.move_left();
	if event.key == "ArrowUp" then tetromino.rotate_right();
	if event.key == "ArrowDown" then tetromino.rotate_left();
	if event.key == " " and not soft_drop_locked then gravity = 50;

	if playfield.check_collision(tetromino) then {
		tetromino.undo();
	}

	# render now, so we can see the rotated/moved tetromino instantly
	renderer.render(playfield, tetromino, pool.next(), score, level, lines);
});

setEventHandler("canvas.keyup", function(event){
	if event.key == " " then {
		soft_drop_locked = false;
		gravity = level_gravity[level];
	}
});

enterEventMode();